eventNodeClick = (e) ->
  isOpen = (node) ->
    for open_id in $tree.tree('getState')['open_nodes']
      return true if open_id == node.id
    return false

  node = e.node

  if selected_node = $tree.tree('getSelectedNode')
    if selected_node.id == node.id
      window.location = "<%= ::Rails.application.routes.url_helpers.things_path %>/#{node.id}"
      return

  if parent = node.parent
    for sibling in parent.children
      continue if sibling.id == node.id
      $tree.tree('toggle', sibling) if isOpen(sibling)

  $tree.tree('toggle', node) unless isOpen(node)

eventNodeMove = (e) ->
  moved_node = e.move_info.moved_node
  target_node = e.move_info.target_node
  position = e.move_info.position
  path_template = "<%= ::Rails.application.routes.url_helpers.move_to_thing_path('-ID-') %>"
  path = path_template.replace('-ID-', "#{moved_node.id}")
  jqXHR = $.raj.handleAjaxRequest
    url: path
    type: 'post'
    dataType: 'json'
    data:
      position: position
      target_id: target_node.id


$(document).on "ready page:change", ->
  window.$tree = $('#tree')
  $tree.tree
    dragAndDrop: false
    autoOpen: 1000
    useContextMenu: false
    selectable: true
    autoEscape: false

  $tree.bind 'tree.click', eventNodeClick
  $tree.bind 'tree.move', eventNodeMove